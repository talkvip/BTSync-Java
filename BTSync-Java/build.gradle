apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'ms.safi.btsync'
version = '0.1'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.jboss.resteasy',	name: 'resteasy-jaxrs',				version: '3.0.7.Final'
    compile group: 'org.jboss.resteasy',	name: 'resteasy-client',			version: '3.0.7.Final'
    compile group: 'org.jboss.resteasy',	name: 'resteasy-jackson-provider',	version: '3.0.7.Final'
    compile group: 'org.apache.commons',	name: 'commons-lang3',				version: '3.3.2'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
			
			pom.artifactId = 'btsync-java'
			
			pom.project {
				name 'BTSync-Java'
				packaging 'jar'
				description 'A Java wrapper library for the BitTorrent Sync API'
				url 'http://safi.ms'
				
				scm {
					url 'https://bitbucket.org/osafi/btsync-java.git'
					connection 'scm:git:ssh://git@bitbucket.org/osafi/btsync-java.git'
					developerConnection 'scm:git:ssh://git@bitbucket.org/osafi/btsync-java.git'
				}
				
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				
				developers {
					developer {
						id 'osafi'
						name 'Omeed Safi'
						email 'safi.omeed@gmail.com'
					}
				}
			}
		}
	}
}